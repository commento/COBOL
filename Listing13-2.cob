IDENTIFICATION DIVISION.
PROGRAM-ID. LISTING13-2.

DATA DIVISION.
WORKING-STORAGE SECTION.
01     STATESTABLE.
       02  STATEVALUES.
           03  FILLER PIC X(60)
               VALUE "ALALABAMA       MONTGOMERY    AKALASKA        JUNEAU".
           03  FILLER PIC X(60)
               VALUE "AZARIZONA       PHOENIX       ARARKANSAS      LITTLE ROCK".
           03  FILLER PIC X(60)
               VALUE "CACALIFORNIA    SACRAMENTO    COCOLORADO      DENVER".
           03  FILLER PIC X(60)
               VALUE "CTCONNECTICUT   HARTFORD      DEDELAWARE      DOVER".
           03  FILLER PIC X(60)
               VALUE "FLFLORIDA       TALLAHASSE    GAGEORGIA       ATLANTA".
           03  FILLER PIC X(60)
               VALUE "HIHAWAII        HONOLULU      IDIDAHO         BOISE".
           03  FILLER PIC X(60)
               VALUE "ILILLINOIS      SPRINGFIELD   ININDIANA       INDIANAPOLIS".
           03  FILLER PIC X(60)
               VALUE "IAIOWA          DES MOINES    KSKANSAS        TOPEKA".
           03  FILLER PIC X(60)
               VALUE "KYKENTUCKY      FRANKFORT     LALOUISIANA     BATON ROUGE".
           03  FILLER PIC X(60)
               VALUE "MEMAINE         AUGUSTA       MDMARYLAND      ANNAPOLIS".
           03  FILLER PIC X(60)
               VALUE "MAMASSACHUSETTS BOSTON        MIMICHIGAN      LANSING".
           03  FILLER PIC X(60)
               VALUE "MNMINNESOTA     SAINT PAUL    MSMISSISSIPPI   JACKSON".
           03  FILLER PIC X(60)
               VALUE "MOMISSOURI      JEFFERSON CITYMTMONTANA       HELENA".
           03  FILLER PIC X(60)
               VALUE "NENEBRASKA      LINCOLN       NVNEVADA        CARSON CITY".
           03  FILLER PIC X(60)
               VALUE "NHNEW HAMPSHIRE CONCORD       NJNEW JERSEY    TRENTON".
           03  FILLER PIC X(60)
               VALUE "NMNEW MEXICO    SANTA FE      NYNEW YORK      ALBANY".
           03  FILLER PIC X(60)
               VALUE "NCNORTH CAROLINARALEIGH       NDNORTH DAKOTA  BISMARCK".
           03  FILLER PIC X(60)
               VALUE "OHOHIO          COLUMBUS      OKOKLAHOMA      OKLAHOMA CITY".
           03  FILLER PIC X(60)
               VALUE "OROREGON        SALEM         PAPENNSYLVANIA  HARRISBURG".
           03  FILLER PIC X(60)
               VALUE "RIRHODE ISLAND  PROVIDENCE    SCSOUTH CAROLINACOLUMBIA".
           03  FILLER PIC X(60)
               VALUE "SDSOUTH DAKOTA  PIERRE        TNTENNESSEE     NASHVILLE".
           03  FILLER PIC X(60)
               VALUE "TXTEXAS         AUSTIN        UTUTAH          SALT LAKE CITY".
           03  FILLER PIC X(60)
               VALUE "VTVERMONT       MONTPELIER    VAVIRGINIA      RICHMOND".
           03  FILLER PIC X(60)
               VALUE "WAWASHINGTON    OLYMPIA       WVWEST VIRGINIA CHARLESTON".
           03  FILLER PIC X(60)
               VALUE "WIWISCONSIN     MADISON       WYWYOMING       CHEYENNE".
       02 FILLER REDEFINES STATEVALUES.
           03  STATE OCCURS 50 TIMES INDEXED BY STATEIDX.
               04  STATECODE       PIC XX.
               04  STATENAME       PIC X(14).
               04  STATECAPITAL    PIC X(14).

01     STATENAMEIN                 PIC X(14).

01     STATECAPITALIN              PIC X(14).

01     STATECODEIN                 PIC XX.

01     SEARCHCHOICE                PIC 9 VALUE ZERO.
       88  VALIDSEARCHCHOICE       VALUES 1, 2, 3, 4.
       88  ENDOFINPUT              VALUE 4.

PROCEDURE DIVISION.
BEGIN.
       PERFORM WITH TEST AFTER UNTIL ENDOFINPUT
           PERFORM WITH TEST AFTER UNTIL VALIDSEARCHCHOICE
               DISPLAY SPACES
               DISPLAY "SEARCH BY STATECODE (1), STATENAME (2), STATECAPITAL (3), STOP (4) - "
                   WITH NO ADVANCING
               ACCEPT SEARCHCHOICE
           END-PERFORM
           SET STATEIDX TO 1
           EVALUATE SEARCHCHOICE
               WHEN 1 PERFORM GETNAMEANDCAPITAL
               WHEN 2 PERFORM GETCODEANDCAPITAL
               WHEN 3 PERFORM GETCODEANDNAME
           END-EVALUATE
       END-PERFORM
       STOP RUN.

GETNAMEANDCAPITAL.
       DISPLAY "ENTER THE TWO LETTER STATE CODE - " WITH NO ADVANCING
       ACCEPT STATECODEIN
       MOVE FUNCTION UPPER-CASE(STATECODEIN) TO STATECODEIN
       SEARCH STATE AT END DISPLAY "STATE CODE " STATECODEIN " DOES NOT EXIST"
           WHEN STATECODE(STATEIDX) = STATECODEIN
               DISPLAY "STATE NAME    = " STATENAME(STATEIDX)
               DISPLAY "STATE CAPITAL = " STATECAPITAL(STATEIDX)
       END-SEARCH.

GETCODEANDCAPITAL.
       DISPLAY "ENTER THE STATE NAME - " WITH NO ADVANCING
       ACCEPT STATENAMEIN
       SEARCH STATE AT END DISPLAY "STATE NAME " STATENAMEIN " DOES NOT EXIST"
           WHEN FUNCTION UPPER-CASE(STATENAME(STATEIDX)) = FUNCTION UPPER-CASE (STATENAMEIN)
               DISPLAY "STATE CODE    = " STATECODE(STATEIDX)
               DISPLAY "STATE CAPITAL = " STATECAPITAL(STATEIDX)
       END-SEARCH.

GETCODEANDNAME.
       DISPLAY "ENTER THE STATE CAPITAL - " WITH NO ADVANCING
       ACCEPT STATECAPITALIN
       SEARCH STATE AT END DISPLAY "STATE CAPITAL " STATECAPITALIN " DOES NOT EXIST"
           WHEN FUNCTION UPPER-CASE(STATECAPITAL(STATEIDX)) = FUNCTION UPPER-CASE (STATECAPITALIN)
               DISPLAY "STATE CODE    = " STATECODE(STATEIDX)
               DISPLAY "STATE NAME    = " STATENAME(STATEIDX)
       END-SEARCH.

