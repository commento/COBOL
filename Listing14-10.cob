IDENTIFICATION DIVISION.
PROGRAM-ID. LISTING14-10.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
       SELECT WORKFILE ASSIGN TO "WORKFILE.TMP".

       SELECT INPUTFILE ASSIGN TO "Listing14-10.dat"
           ORGANIZATION IS LINE SEQUENTIAL.

       SELECT OUTPUTFILE ASSIGN TO "Listing14-10.rpt"
           ORGANIZATION IS LINE SEQUENTIAL.

DATA DIVISION.
FILE SECTION.
FD     INPUTFILE.
01     INPUTREC.
       88  ENDOFINPUTFILE          VALUE HIGH-VALUES.
       02  GUESTNAMEIF             PIC X(20).
       02  COUNTRYNAMEIF           PIC X(20).
           88  IRELAND             VALUE "Ireland".
       02  FILLER                  PIC X(40).

SD     WORKFILE.
01     WORKREC.
       88  ENDOFWORKFILE           VALUE HIGH-VALUES.
       02  COUNTRYNAMEWF           PIC X(20).
       02  GUESTNAMEWF             PIC X(20).       


FD     OUTPUTFILE.
01     OUTPUTLINE                  PIC X(40).

WORKING-STORAGE SECTION.

01     TITLELINE                   PIC X(40) 
                                   VALUE "       FOREIGN GUESTS REPORT        ".

01     SUBJECTLINE                 PIC X(40) 
                                   VALUE "COUNTRY           VISITORS".

01     FOOTERLINE                  PIC X(40) 
                                   VALUE "       *****ENDOFREPORT*****        ".

PROCEDURE DIVISION.
BEGIN.
       SORT WORKFILE ON ASCENDING COUNTRYNAMEWF
           INPUT PROCEDURE IS PRESORTFILTER
           OUTPUT PROCEDURE IS WRITETOREPORT 
       STOP RUN.

PRESORTFILTER.
       OPEN INPUT INPUTFILE
       READ INPUTFILE AT END SET ENDOFINPUTFILE TO TRUE
       END-READ
       PERFORM UNTIL ENDOFINPUTFILE
           IF NOT IRELAND
               MOVE GUESTNAMEIF TO GUESTNAMEWF
               MOVE COUNTRYNAMEIF TO COUNTRYNAMEWF
               RELEASE WORKREC
           END-IF
           READ INPUTFILE AT END SET ENDOFINPUTFILE TO TRUE
           END-READ
       END-PERFORM
       CLOSE INPUTFILE.

WRITETOREPORT.
       OPEN OUTPUT OUTPUTFILE
       WRITE OUTPUTLINE FROM TITLELINE AFTER ADVANCING 1 LINE 
       WRITE OUTPUTLINE FROM SUBJECTLINE AFTER ADVANCING 1 LINE
       RETURN WORKFILE AT END SET ENDOFWORKFILE TO TRUE
       END-RETURN
       PERFORM UNTIL ENDOFWORKFILE
           WRITE OUTPUTLINE FROM WORKREC AFTER ADVANCING 1 LINE
           RETURN WORKFILE AT END SET ENDOFWORKFILE TO TRUE
           END-RETURN
       END-PERFORM
       WRITE OUTPUTLINE FROM FOOTERLINE AFTER ADVANCING 1 LINE
       CLOSE OUTPUTFILE.
