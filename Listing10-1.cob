IDENTIFICATION DIVISION.
PROGRAM-ID. LISTING10-1.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
SELECT SALESFILE ASSIGN TO "Listing10-1.dat"
       ORGANIZATION IS LINE SEQUENTIAL.

SELECT SALESREPORT ASSIGN TO "Listing10-1.RPT"
       ORGANIZATION IS LINE SEQUENTIAL.

DATA DIVISION.
FILE SECTION.
FD     SALESFILE.
01     SALESRECORD.
       88  ENDOFSALESFILE    VALUE HIGH-VALUES.
       02  STATENAME           PIC X(14).
       02  BRANCHID            PIC X(5).
       02  SALESPERSONID       PIC X(6).
       02  VALUEOFSALES        PIC 9(4)V99.

FD     SALESREPORT.
01     PRINTLINE               PIC X(55).

WORKING-STORAGE SECTION.
01     REPORTHEADING.
       02 FILLER               PIC X(35)
           VALUE "        ELECTRONICS2GO SALES REPORT".
01     SUBJECTHEADING.
       02 FILLER               PIC X(43)
           VALUE "STATE NAME    BRANCH   SALESID  SALESTOTAL".

01     DETAILLINE.
       02 PRNSTATENAME         PIC X(14).
        88 SUPPRESSSTATENAME   VALUE SPACES.
       02 PRNBRANCHID          PIC BBX(5).
        88 SUPPRESSBRANCHID    VALUE SPACES.
       02 PRNSALESPERSONID     PIC BBBBX(6).
       02 PRNSALESPERSONTOTAL  PIC BB$$,$$9.99.

01     BRANCHTOTALLINE.
       02 FILLER               PIC X(43)
           VALUE "                         BRANCH TOTAL :    ".
       02 PRNBRANCHTOTAL       PIC $$$,$$9.99.

01     STATETOTALLINE.
       02 FILLER               PIC X(40)
           VALUE "                        STATE TOTAL :  ".
       02 PRNSTATETOTAL        PIC $$,$$$,$$9.99.

01     FINALTOTALLINE.
       02 FILLER               PIC X(39)
           VALUE "                         FINAL TOTAL :".
       02 PRNFINALTOTAL        PIC $$$,$$$,$$9.99.

01     SALESPERSONTOTAL        PIC 9(4)V99.
01     BRANCHTOTAL             PIC 9(6)V99.
01     STATETOTAL              PIC 9(7)V99.
01     FINALTOTAL              PIC 9(9)V99.

01     PREVSTATENAME           PIC X(14).
01     PREVBRANCHID            PIC X(5).
01     PREVSALESPERSONID       PIC X(6).

PROCEDURE DIVISION.
BEGIN.
       OPEN INPUT SALESFILE
       OPEN OUTPUT SALESREPORT
       WRITE PRINTLINE FROM REPORTHEADING AFTER ADVANCING 1 LINE
       WRITE PRINTLINE FROM SUBJECTHEADING AFTER ADVANCING 1 LINE

       READ SALESFILE
           AT END SET ENDOFSALESFILE TO TRUE
       END-READ
       PERFORM SUMSALESFINALTOTAL UNTIL ENDOFSALESFILE
       MOVE FINALTOTAL TO PRNFINALTOTAL
       WRITE PRINTLINE FROM FINALTOTALLINE AFTER ADVANCING 1 LINE.

       CLOSE SALESFILE, SALESREPORT
       STOP RUN.

SUMSALESFINALTOTAL.
       MOVE STATENAME TO PREVSTATENAME, PRNSTATENAME
       MOVE ZEROS TO STATETOTAL
       PERFORM SUMSALESFORSTATE UNTIL STATENAME NOT = PREVSTATENAME OR ENDOFSALESFILE
       MOVE STATETOTAL TO PRNSTATETOTAL
       WRITE PRINTLINE FROM STATETOTALLINE AFTER ADVANCING 1 LINE.


SUMSALESFORSTATE.
       WRITE PRINTLINE FROM SPACES AFTER ADVANCING 1 LINE
       MOVE BRANCHID TO PREVBRANCHID, PRNBRANCHID
       MOVE ZEROS TO BRANCHTOTAL
       PERFORM SUMSALESFORBRANCH
           UNTIL BRANCHID NOT = PREVBRANCHID
             OR STATENAME NOT = PREVSTATENAME
             OR ENDOFSALESFILE
       MOVE BRANCHTOTAL TO PRNBRANCHTOTAL
       WRITE PRINTLINE FROM BRANCHTOTALLINE AFTER ADVANCING 1 LINE.

SUMSALESFORBRANCH.
       MOVE SALESPERSONID TO PREVSALESPERSONID, PRNSALESPERSONID
       MOVE ZEROS TO SALESPERSONTOTAL
       PERFORM SUMSALESPERSONSALES
           UNTIL SALESPERSONID NOT = PREVSALESPERSONID
             OR BRANCHID NOT = PREVBRANCHID
             OR STATENAME NOT = PREVSTATENAME
             OR ENDOFSALESFILE
       MOVE SALESPERSONTOTAL TO PRNSALESPERSONTOTAL
       WRITE PRINTLINE FROM DETAILLINE AFTER ADVANCING 1 LINE
       SET SUPPRESSBRANCHID TO TRUE
       SET SUPPRESSSTATENAME TO TRUE.

SUMSALESPERSONSALES.
       ADD VALUEOFSALES TO SALESPERSONTOTAL, BRANCHTOTAL, STATETOTAL, FINALTOTAL
       READ SALESFILE 
           AT END SET ENDOFSALESFILE TO TRUE
       END-READ.
