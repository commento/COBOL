IDENTIFICATION DIVISION.
PROGRAM-ID. LISTING13-5.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
       SELECT COUNTRYCODEFILE ASSIGN TO "Listing13-5.dat"
           ORGANIZATION IS LINE SEQUENTIAL.

DATA DIVISION.
FILE SECTION.
FD     COUNTRYCODEFILE.
01     COUNTRYCODEREC.
       88  ENDOFCOUNTRYCODEFILE    VALUE HIGH-VALUES.
       02  COUNTRYCODECF           PIC XX.
       02  COUNTRYNAMECF           PIC X(25).

WORKING-STORAGE SECTION.
01     COUNTRYCODETABLE.
       02  COUNTRY OCCURS 249 TIMES
           ASCENDING KEY IS COUNTRYCODE
           INDEXED BY CIDX.
           03  COUNTRYCODE         PIC XX.
           03  COUNTRYNAME         PIC X(25).

01     COUNTRYCODEIN               PIC XX.
       88  ENDOFINPUT              VALUE SPACES.

01     FILLER                      PIC 9 VALUE ZERO.
       88  VALIDCOUNTRYCODE        VALUE 1.

PROCEDURE DIVISION.
BEGIN.
       PERFORM LOADCOUNTRYTABLE
       PERFORM WITH TEST AFTER UNTIL ENDOFINPUT
           PERFORM WITH TEST AFTER UNTIL VALIDCOUNTRYCODE OR ENDOFINPUT
               DISPLAY "ENTER A COUNTRY CODE (SPACE TO STOP) - "
                   WITH NO ADVANCING
               ACCEPT COUNTRYCODEIN
               SEARCH ALL COUNTRY
                   AT END IF NOT ENDOFINPUT
                               DISPLAY "COUNTRY CODE " COUNTRYCODEIN " IS NOT VALID"
                          END-IF
                   WHEN COUNTRYCODE(CIDX) = FUNCTION UPPER-CASE(COUNTRYCODEIN)
                   DISPLAY COUNTRYCODEIN " IS " COUNTRYNAME(CIDX)
               END-SEARCH
               DISPLAY SPACES
           END-PERFORM
       END-PERFORM
       STOP RUN.

LOADCOUNTRYTABLE.
       MOVE HIGH-VALUES TO COUNTRYCODETABLE
       OPEN INPUT COUNTRYCODEFILE
       READ COUNTRYCODEFILE
           AT END SET ENDOFCOUNTRYCODEFILE TO TRUE
       END-READ

       PERFORM VARYING CIDX FROM 1 BY 1 UNTIL ENDOFCOUNTRYCODEFILE
           MOVE COUNTRYCODEREC TO COUNTRY(CIDX)
           READ COUNTRYCODEFILE
               AT END SET ENDOFCOUNTRYCODEFILE TO TRUE
           END-READ
       END-PERFORM
       CLOSE COUNTRYCODEFILE.
