IDENTIFICATION DIVISION.
PROGRAM-ID. LISTING13-7.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
       SELECT WORDFILE ASSIGN TO "Listing13-7.dat"
           ORGANIZATION IS LINE SEQUENTIAL.

DATA DIVISION.
FILE SECTION.
FD     WORDFILE.
01     WORDIN           PIC X(20).
       88  ENDOFWORDFILE   VALUE HIGH-VALUES.

WORKING-STORAGE SECTION.
01     WORDFREQTABLE.
       02  WORD OCCURS 0 TO 2000 TIMES
                       DEPENDING ON NUMBEROFWORDS
                       INDEXED BY WIDX.
           03 WORDFOUND    PIC X(20).
           03 WORDFREQ     PIC 9(3).

01     TOPTENTABLE.
       02  WORDTT OCCURS 11 TIMES INDEXED BY TTIDX.
           03  WORDFOUNDTT PIC X(20) VALUE SPACES.
           03  WORDFREQTT  PIC 9(3)  VALUE ZEROS.

01     NUMBEROFWORDS       PIC 9(4)  VALUE ZEROS.

01     REPORTHEADER        PIC X(27) VALUE "  TOP TEN WORDS IN DOCUMENT".

01     SUBJECTHEADER       PIC X(29) VALUE "POS    OCCURS   DOCUMENT WORD".

01     DETAILLINE.
       02  PRNPOS          PIC Z9.
       02  FILLER          PIC X VALUE ".".
       02  PRNFREQ         PIC BBBBBZZ9.
       02  PRNWORD         PIC BBBBBX(20).

01     POS                 PIC 99.

PROCEDURE DIVISION.
BEGIN.
       OPEN INPUT WORDFILE
       READ WORDFILE AT END SET ENDOFWORDFILE TO TRUE
       END-READ
       PERFORM LOADWORDFREQTABLE UNTIL ENDOFWORDFILE
       PERFORM FINDTOPTENWORDS VARYING WIDX FROM 1 BY 1 UNTIL WIDX > NUMBEROFWORDS
       PERFORM DISPLAYTOPTENWORDS
       CLOSE WORDFILE
       STOP RUN.

LOADWORDFREQTABLE.
       SET WIDX TO 1
       SEARCH WORD AT END ADD 1 TO NUMBEROFWORDS
               MOVE 1 TO WORDFREQ(WIDX)
               MOVE FUNCTION LOWER-CASE(WORDIN) TO WORDFOUND(WIDX)
           WHEN FUNCTION LOWER-CASE(WORDIN) = WORDFOUND(WIDX)
               ADD 1 TO WORDFREQ(WIDX)
       END-SEARCH
       READ WORDFILE AT END SET ENDOFWORDFILE TO TRUE
       END-READ.

FINDTOPTENWORDS.
       PERFORM VARYING TTIDX FROM 10 BY -1 UNTIL TTIDX < 1
           IF WORDFREQ(WIDX) > WORDFREQTT(TTIDX)
               MOVE WORDTT(TTIDX) TO WORDTT(TTIDX + 1)
               MOVE WORD(WIDX) TO WORDTT(TTIDX)
           END-IF
       END-PERFORM.

DISPLAYTOPTENWORDS.
       DISPLAY REPORTHEADER
       DISPLAY SUBJECTHEADER
       PERFORM VARYING TTIDX FROM 1 BY 1 UNTIL TTIDX > 10
           SET POS TO TTIDX
           MOVE POS TO PRNPOS
           MOVE WORDFOUNDTT(TTIDX) TO PRNWORD
           MOVE WORDFREQTT(TTIDX) TO PRNFREQ
           DISPLAY DETAILLINE
       END-PERFORM.
